// Cryptol CBC Implementation
// Copyright (c) 2010-2018, Galois Inc.
// www.cryptol.net
// Author: Ajay Kumar Eeralla

module Primitive::Symmetric::Cipher::Block::Modes::CTR where

// set the sizes
type ic = [128]
type block = [128]


ctrEnc : {n, k} (fin n, fin k) => ([k]->block -> block) -> [k] -> ic -> [n]block -> [n]block
ctrEnc enc k c ps = cs
    where cs = [(enc k c')^p | p <- ps | c' <- ctrs]
          ctrs = [c+i | i<- [0...]]

ctrDec : {n, k} (fin n, fin k) => ([k]->block -> block) -> [k] -> ic -> [n]block -> [n]block
ctrDec enc k c cs = ps
    where ps = [(enc k c')^ct | ct <- cs | c' <- ctrs]
          ctrs = [c+i | i<- [0...]]

// This will need to be parameterized with a specific encryption function before it can be proved
property ctrEncCorrect enc k c ps = (ctrDec enc k c (ctrEnc enc k c ps)) == ps


